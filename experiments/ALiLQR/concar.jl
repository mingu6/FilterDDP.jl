using IterativeLQR 
using LinearAlgebra
using Plots
using Random
using BenchmarkTools
using Printf

benchmark = false
verbose = true

N = 101
Δ = 0.05
r_car = 0.02
xN = [1.0; 1.0; π / 4; 0.0]

options = Options()
options.scaling_penalty = 3.0
options.max_dual_updates = 300
options.constraint_tolerance = 1e-8

# ## car 
num_state = 4
num_control = 2

# ## control limits

ul = [-2.0; -4.0]
uu = [2.0; 4.0]

# ## obstacles

xyr_obs = [
    [0.05, 0.25, 0.1],
    [0.45, 0.1, 0.15],
    [0.7, 0.7, 0.2],
    [0.30, 0.4, 0.1]
    ]
num_obstacles = length(xyr_obs)

include("../visualise/concar.jl")

# ## Dynamics - RK4

# continuous time dynamics
function g(x, u)
    [x[4] * cos(x[3]); x[4] * sin(x[3]); u[2]; u[1]]
end

function RK4(x, u, g)
    k1 = g(x, u)
    k2 = g(x + Δ * 0.5 * k1, u)
    k3 = g(x + Δ * 0.5 * k2, u)
    k4 = g(x + Δ * k3, u)
    return x + Δ / 6 * (k1 + k2 + k3 + k4)
end

f = (x, u) -> RK4(x, u, g)

car = Dynamics(f, num_state, num_control)
dynamics = [car for k = 1:N-1] 

# ## objective 

stage_cost = (x, u) -> begin
    J = 0.0
    J += Δ * dot(x - xN, x - xN)
    J += Δ * dot(u[1:2] .* [10.0, 1.0], u[1:2])
    return J
end
term_cost = (x, u) -> 1e3 * dot(x - xN, x - xN)
objective = [
    [Cost(stage_cost, num_state, num_control) for k = 1:N-1]...,
    Cost(term_cost, num_state, 0)
]

function eval_objective(x, u)
    J = 0.0
    for t = 1:N-1
        J += stage_cost(x[t], u[t])
    end
    J += term_cost(x[N], 0.0)
end

# ## constraints - waypoints are constraints for iLQR

obs_dist(obs_xy) = (x) -> begin
    xy_diff = x[1:2] - obs_xy
    return dot(xy_diff, xy_diff)
end
path_constr_fn = (x, u) -> begin
[
    ul - u; ## control limit (lower)
    u - uu; ## control limit (upper)
    # obstacle avoidance constraints i.e., d_thresh^2 - d_obs^2 <= 0 
    [(obs[3] + r_car)^2 - obs_dist(obs[1:2])(f(x, u))
        for (i, obs) in enumerate(xyr_obs)];
    # bound constraints, car must stay within [0, 1] x [0, 1] box
    -f(x, u)[1];
    -f(x, u)[2];
    f(x, u)[1] - 1.0;
    f(x, u)[2] - 1.0;
]
end

obs_constr = Constraint(path_constr_fn, num_state, num_control,
    indices_inequality=collect(1:2*num_control+num_obstacles+4))

constraints = [[obs_constr for k = 1:N-1]..., Constraint()]

function eval_constraints_1norm(x, u)
    θ = 0.0
    for t in 1:N-1
        h = path_constr_fn(x[t], u[t])
        θ += norm(max.(h, zeros(length(h))), 1)
    end
    return θ
end

# ## Initialise solver

solver = Solver(dynamics, objective, constraints; options=options)

# ## Plots

plot(xlims=(0, 1), ylims=(0, 1), xtickfontsize=14, ytickfontsize=14)
for xyr in xyr_obs
    plotCircle!(xyr[1], xyr[2], xyr[3])
end

open("results/concar.txt", "w") do io
	@printf(io, " seed  iterations  status     objective           primal        wall (ms)  solver (ms)  \n")
	for seed = 1:50
        solver = Solver(dynamics, objective, constraints; options=options)
		solver.options.verbose = verbose
		Random.seed!(seed)
		
        # ## Initialise solver and solve
        
        x0 = rand(4) .* [0.05; 0.05; π / 2; 0.0]
        ū = [1.0e-3 .* (rand(2) .- 0.5) for k = 1:N-1]
        x̄ = rollout(dynamics, x0, ū)
        
        solve!(solver, x̄, ū)
        
        x_sol, u_sol = get_trajectory(solver)
        plotTrajectory!(x_sol)

        J = eval_objective(x_sol, u_sol)
        θ = eval_constraints_1norm(x_sol, u_sol)
		
		if benchmark
            solver.options.verbose = false
            solve_time = @belapsed solve!($solver, $x̄, $ū) samples=10 setup=(solver=Solver(dynamics, objective, constraints; options=options))
            @printf(io, " %2s     %5s      %5s    %.8e    %.8e    %5.1f       %5.1f\n", seed, solver.data.iterations[1], solver.data.status[1], J, θ, solve_time * 1000, 0.0)
        else
            @printf(io, " %2s     %5s      %5s    %.8e    %.8e \n", seed, solver.data.iterations[1], solver.data.status[1], J, θ)
        end
    end
end

savefig("plots/concar_ALiLQR.pdf")
